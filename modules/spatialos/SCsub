#!/usr/bin/env python

Import('env')

module_env = env.Clone()

# Explicit listing of source files for this module.
src_list = [
    "spatial_util.cpp",
    "position_sync.cpp",
    "player_controls_sync.cpp",
    "world_view.cpp",
    "entity_view.cpp",
    "component_view.cpp",
    "spatialos.cpp",
    "register_types.cpp",
    "snapshot_generator.cpp",
    "worker_logger.cpp",
    "worker_logger_wrapper.cpp",
    "component_registry.cpp"]
module_env.add_source_files(env.modules_sources, src_list)

# Schema dependencies.
module_env.Append(CPPPATH="generated_code/cpp")
module_env.Append(CPPPATH="generated_code/cpp/improbable")
module_env.Append(CPPPATH="generated_code/cpp/spellcrest")
module_env.Append(CPPPATH="generated_code/cpp/godotcore")
# On msvc builds these need to be added to the global environment path
if (env.msvc):
    env.Append(CPPPATH="generated_code/cpp")
    env.Append(CPPPATH="generated_code/cpp/improbable")
    env.Append(CPPPATH="generated_code/cpp/spellcrest")
    env.Append(CPPPATH="generated_code/cpp/godotcore")

# Set compiler options as the worker sdk requires c++11
if (not env.msvc):
    module_env.Append(CXXFLAGS=['-O2', '-std=c++11', '-g'])

# Worker SDK include path and libraries.
# Note that the environment, rather than the module, is what needs the sdk libraries.
module_env.Append(CPPPATH="worker_sdk/cpp/include")
env.Append(LIBPATH=["worker_sdk/cpp/lib"])
if (not env.msvc):
    # On linux we depend on cpp-static-x86_64-gcc_libstdcpp-linux
    env.Append(LIBS=Glob("worker_sdk/cpp/lib/*.a"))
else:
    # On windows we depend on cpp-static-x86-msvc_md-win32
    # NOTE: the build instructions are different to out-of-the-box godot (we compile with MD instead of MT)
    env.Append(CPPPATH="worker_sdk/cpp/include")
    env.Append(LIBS=Glob("worker_sdk/cpp/lib/*.lib"))


# Build schema into a static library
if (not env.msvc):
    improbable_schema = Glob("generated_code/cpp/improbable/*.h", strings=True) + Glob("generated_code/cpp/improbable/*.cc", strings=True)
    spellcrest_schema = Glob("generated_code/cpp/spellcrest/*.h", strings=True) + Glob("generated_code/cpp/spellcrest/*.cc", strings=True)
    godotcore_schema = Glob("generated_code/cpp/godotcore/*.h", strings=True) + Glob("generated_code/cpp/godotcore/*.cc", strings=True)
    schemaLib = module_env.Library(target='#bin/schema', source=(improbable_schema + spellcrest_schema + godotcore_schema))
    shim = schemaLib[0].name.rsplit('.', 1)[0]
    env.Append(LIBS=[shim])
    env.Append(LIBPATH=['#bin'])
else:
    improbable_schema = Glob("generated_code/cpp/improbable/*.cc", strings=True)
    spellcrest_schema = Glob("generated_code/cpp/spellcrest/*.cc", strings=True)
    godotcore_schema = Glob("generated_code/cpp/godotcore/*.cc", strings=True)
    lib = env.add_library("schema", improbable_schema + spellcrest_schema + godotcore_schema)
    env.Append(LIBS=[lib])
    env.Append(LIBPATH=['schema'])

Export('env')
